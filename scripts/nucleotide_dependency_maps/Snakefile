configfile: "config.yaml"


assert config["START_POSITION"] < config["END_POSITION"]
assert (config["END_POSITION"] - config["START_POSITION"]) - config[
    "DISPLAY_MAP_START_RELATIVE"
] + config["DISPLAY_MAP_END_RELATIVE"] + 1 > 0


rule all:
    input:
        expand(
            "output/display_dependency_map/{model}/{sequence}/{chromosome}/seq_{seq_start}-{seq_end}/dp_{map_start}-{map_end}.png",
            model=config["MODELS"],
            sequence=config["SEQUENCES"],
            chromosome=config["CHROMOSOME"],
            seq_start=config["START_POSITION"],
            seq_end=config["END_POSITION"],
            map_start=config["DISPLAY_MAP_START_RELATIVE"],
            map_end=config["DISPLAY_MAP_END_RELATIVE"],
        ),


rule download_fasta:
    params:
        url=lambda wildcards: config["FASTA_URL"].get(wildcards.sequence),
    output:
        "output/download_fasta/{sequence}.fna.gz",
    shell:
        "wget --no-check-certificate {params.url} -O {output}"


rule compute_dependency_map:
    input:
        "output/download_fasta/{sequence}.fna.gz",
    params:
        model=lambda wildcards: wildcards.model,
    output:
        "output/compute_dependency_map/{model}/{sequence}/{chromosome}/seq_{seq_start}-{seq_end}.parquet",
    script:
        "scripts/compute_dependency_map.py"


rule display_dependency_map:
    input:
        "output/download_fasta/{sequence}.fna.gz",
        "output/compute_dependency_map/{model}/{sequence}/{chromosome}/seq_{seq_start}-{seq_end}.parquet",
    output:
        "output/display_dependency_map/{model}/{sequence}/{chromosome}/seq_{seq_start}-{seq_end}/dp_{map_start}-{map_end}.{format}",
    script:
        "scripts/display_dependency_map.py"
